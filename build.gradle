import org.springframework.boot.gradle.plugin.SpringBootPlugin

buildscript {
    ext {
        springBootVersion = '2.3.4.RELEASE'
    }
}
plugins {
    id 'java'
    id 'eclipse'
    id 'org.springframework.boot' version "${springBootVersion}"
    id 'io.freefair.lombok' version '5.2.1'
}

configurations {
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
}
group = 'com.odde'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    implementation platform(SpringBootPlugin.BOM_COORDINATES)
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation(
            'mysql:mysql-connector-java:8.0.21',
            'io.jsonwebtoken:jjwt:0.9.1',
            'javax.xml.bind:jaxb-api:2.3.1'
    )
    developmentOnly("org.springframework.boot:spring-boot-devtools:${springBootVersion}")
    testImplementation(
            "org.springframework.boot:spring-boot-starter-test",
            "org.springframework.security:spring-security-test",
    )
}

test {
    useJUnitPlatform()
    testLogging {
        events 'FAILED', 'SKIPPED'
    }
    afterSuite { desc, result ->
        if (!desc.parent) {
            println "\nTest result: ${result.resultType}"
            println "Test summary: ${result.testCount} tests, " +
                    "${result.successfulTestCount} succeeded, " +
                    "${result.failedTestCount} failed, " +
                    "${result.skippedTestCount} skipped"
        }
    }
}

task bootRunTest {
}

bootRunTest.finalizedBy bootRun {
    environment "SPRING_PROFILES_ACTIVE", "test"
}
